/*
 * Copyright 2025 KPG-TB
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
    id "java-library"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.freefair.lombok" version "8.11"

    id "maven-publish"
    id "org.jreleaser" version "1.18.0"
}

group = "dev.projectenhanced"
version = "1.0.0-SNAPSHOT"
description = "EnhancedSpigot is an advanced library that enhances the Spigot API for creating powerful Minecraft plugins"

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jreleaser'

    group = 'dev.projectenhanced'

    java {
        withSourcesJar()
        withJavadocJar()

        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    afterEvaluate {
        shadowJar {
            archiveFileName = project.name + "-" + project.version + ".jar"
            finalizedBy javadoc;
            finalizedBy sourcesJar;
        }


        publishing {
            repositories {
                maven {
                    if (project.version.endsWith("-SNAPSHOT")) {
                        credentials {
                            username project.nexusUsername
                            password project.nexusPassword
                        }
                        url "https://nexus.projectenhanced.dev/repository/maven-snapshots"
                    } else {
                        url layout.buildDirectory.dir('staging-deploy')
                    }
                }
            }

            publications {
                maven(MavenPublication) {
                    groupId = project.group
                    artifactId = project.name

                    from components.java

                    pom {
                        name = project.name
                        description = project.description
                        url = 'https://projectenhanced.dev'
                        inceptionYear = '2025'

                        licenses {
                            license {
                                name = 'Apache-2.0'
                                url = 'https://spdx.org/licenses/Apache-2.0.html'
                            }
                        }

                        developers {
                            developer {
                                id = 'kpgtb'
                                name = 'KPG-TB'
                            }
                        }

                        scm {
                            connection = 'scm:git:https://github.com/KPGTB/EnhancedSpigot.git'
                            developerConnection = 'scm:git:ssh://github.com/KPGTB/EnhancedSpigot.git'
                            url = 'https://github.com/KPGTB/EnhancedSpigot'
                        }
                    }
                }
            }
        }

        jreleaser {
            gitRootSearch = true
            release {
                github {
                    skipRelease = true
                    skipTag = true
                }
            }
            signing {
                active = 'ALWAYS'
                armored = true
                mode = 'FILE'
                publicKey = rootProject.publicKey
                secretKey = rootProject.privateKey
            }
            deploy {
                maven {
                    mavenCentral {
                        sonatype {
                            active = 'ALWAYS'
                            url = 'https://central.sonatype.com/api/v1/publisher'
                            stagingRepository('build/staging-deploy')
                        }
                    }
                }
            }
        }
    }
}